
#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_GOOGLE_include_directive : enable

#include "BRDF.glsl"
#include "common.glsl"


layout(set = 0, binding = 0) uniform  CameraBuffer{
	mat4 viewMatrix;
	mat4 inverseViewMatrix;
  mat4 projectionMatrix;
  mat4 inverseProjectionMatrix;
	vec3 cameraPosition;
} cameraData;

layout(set = 0, binding = 1) uniform  RenderBuffer{
	float fog;
	uint sunLightCount;
	uint pointLightCount;
	uint spotLightCount;
  uint maxRecoursionDepth;
  uint maxDiffuseSampleCount;
  uint maxSpecularSampleCount;
  uint maxShadowRaySampleCount;
  uint noiseSampleTextureIndex;
} globalDrawData;


layout(set = 1, binding = 0) uniform sampler2D primaryRayLitColorTexture;
layout(set = 1, binding = 1) uniform sampler2D primaryRayNormalTexture;
layout(set = 1, binding = 2) uniform sampler2D primaryRayDepthTexture;
layout(set = 1, binding = 3) uniform sampler2D primaryRayRoughnessMetalicTexture;
layout(set = 1, binding = 4) uniform sampler2D primaryRayUnlitColorTexture;

layout(set = 2, binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(set = 2, binding = 1, rgba32f) uniform image2D image;

layout(std430, set = 3, binding = 0) readonly buffer SunBuffer{

	SunLight sunLights[];
} sunLightBuffer;

layout(std430, set = 3, binding = 1) readonly buffer PointBuffer{

	PointLight pointLights[];
} pointLightBuffer;

layout(std430, set = 3, binding = 2) readonly buffer SpotBuffer{

	SpotLight spotLights[];
} spotLightBuffer; 

layout(set = 4, binding = 0) uniform sampler albedoSampler;
layout(set = 4, binding = 1) uniform sampler metallicSampler;
layout(set = 4, binding = 2) uniform sampler normalSampler;
layout(set = 4, binding = 3) uniform sampler roughnessSampler;

layout(std430, set = 5, binding = 0) readonly buffer DrawInstanceBuffer{
	drawInstanceData instanceData[];
} drawData;

layout(set = 6, binding = 0) uniform texture2D textures[1024]; 

struct hitPayload
{
  bool miss;
  vec3 color;
  vec3 normal;
  vec3 position;
};

layout(location = 0) rayPayloadEXT float shadowPayload;
layout(location = 1) rayPayloadEXT hitPayload payload;



void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
    
    vec4 litColor = texture(primaryRayLitColorTexture, inUV);
    vec4 unlitColor = texture(primaryRayUnlitColorTexture, inUV);
    vec4 normal = texture(primaryRayNormalTexture, inUV);
    float depth = texture(primaryRayDepthTexture, inUV).x;
    vec4 roughnessMetalness = texture(primaryRayRoughnessMetalicTexture, inUV);
	
    normal.xyz = normalize(normal.xyz);
	  float reflectance = 0.04;	
	  float roughness = roughnessMetalness.x;
	  float metallic = roughnessMetalness.y;

    vec4 clipSpacePosition = vec4(inUV * 2.0 - 1.0, depth, 1.0);
    vec4 viewSpacePosition = cameraData.inverseProjectionMatrix * clipSpacePosition;
    viewSpacePosition = viewSpacePosition / viewSpacePosition.w;
	  vec4 samplePosition = cameraData.inverseViewMatrix * viewSpacePosition;

    vec3 viewDirection = normalize(cameraData.cameraPosition - samplePosition.xyz);

    uint  rayFlags = gl_RayFlagsOpaqueEXT;
    uint  shadowRayFlags =  gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT;
    float tMin     = 0.001;
    float tMax     = 10000.0;

    vec4 screenNoise = texture(sampler2D(textures[globalDrawData.noiseSampleTextureIndex], albedoSampler), inUV*20.0);

    vec4 outColor = vec4(0.0, 0.0, 0.0, 1.0);
    
    vec3 radiance = vec3(0.0, 0.0, 0.0);

    for(uint i = 0; i < globalDrawData.pointLightCount; i++)
    {
      shadowPayload = 0.0;
      vec3 lightDirection = pointLightBuffer.pointLights[i].position - samplePosition.xyz;
      float distance = length(lightDirection);
      lightDirection = normalize(lightDirection);
      float cosTheta = dot(lightDirection, normal.xyz);
      
      for(uint s = 0; s < globalDrawData.maxShadowRaySampleCount; s++)
      {
        vec4 noise = texture(sampler2D(textures[globalDrawData.noiseSampleTextureIndex], albedoSampler), screenNoise.xy + vec2(float(s), float(s)) + inUV * 20.0);
        
        float radius = pointLightBuffer.pointLights[i].radius;
        float angle = atan(radius/distance);
        lightDirection = createSampleVector(lightDirection, angle, angle, noise.x - 0.5, noise.y - 0.5);

        if(cosTheta > 0)
        {
          traceRayEXT(topLevelAS, // acceleration structure
          shadowRayFlags,       // rayFlags
          0xFF,           // cullMask
          0,              // sbtRecordOffset
          0,              // sbtRecordStride
          1,              // missIndex
          samplePosition.xyz,     // ray origin
          tMin,           // ray min range
          lightDirection,  // ray direction
          distance,           // ray max range
          0               // payload (location = 0)
          );
          vec3 lightColor = normalize(pointLightBuffer.pointLights[i].color.xyz);

	        float attenuation = 1.0/(distance*distance);

	        lightDirection = normalize(lightDirection);

	        vec3 brdf = cookTorranceGgxBRDF(viewDirection, lightDirection, normal.xyz, unlitColor.xyz, metallic, roughness, reflectance) * shadowPayload;

	        radiance =  radiance + max(cosTheta, 0.0) * brdf * lightColor * 50.0 * attenuation;
        }
      }
    }  

   
    for(uint i = 0; i < globalDrawData.sunLightCount; i++)
    {
      shadowPayload = 0.0;
      vec3 lightDirection = -sunLightBuffer.sunLights[i].direction;
      float range = tMax;
      lightDirection = normalize(lightDirection);
      
      float cosTheta = dot(lightDirection, normal.xyz);

      if(cosTheta > 0)
      {
        traceRayEXT(topLevelAS, // acceleration structure
        shadowRayFlags,       // rayFlags
        0xFF,           // cullMask
        0,              // sbtRecordOffset
        0,              // sbtRecordStride
        1,              // missIndex
        samplePosition.xyz,     // ray origin
        tMin,           // ray min range
        lightDirection.xyz,  // ray direction
        range,           // ray max range
        0               // payload (location = 0)
        );

        vec3 lightColor = normalize(sunLightBuffer.sunLights[i].color.xyz);
	      vec3 lightDirection = -normalize(sunLightBuffer.sunLights[i].direction);

	      vec3 brdf = cookTorranceGgxBRDF(viewDirection, lightDirection, normal.xyz, unlitColor.xyz, metallic, roughness, reflectance) * shadowPayload;
	
	      radiance =  radiance + max(cosTheta, 0.0) * brdf * 2.0 * lightColor;
      }
    }  

    outColor.xyz = radiance;

    payload.miss = false;
    payload.position = samplePosition.xyz;
    payload.normal = normal.xyz;
    payload.color = outColor.xyz;

    vec3 priorPosition = cameraData.cameraPosition;

    vec3 glossyIndirectLight = vec3(0.0);

    
    //single bounce diffuse indirect light
    for(uint i = 0; i < globalDrawData.maxDiffuseSampleCount; i++)
    {
      vec4 noise = texture(sampler2D(textures[globalDrawData.noiseSampleTextureIndex], albedoSampler), inUV + vec2(1.0/float(i), 1.0/float(i) + screenNoise.xy));
      vec3 sampleVector = createSampleVector(normal.xyz, M_PI, M_PI * 0.5, noise.x - 0.5, noise.y - 0.5);

      traceRayEXT(topLevelAS, // acceleration structure
        rayFlags,       // rayFlags
        0xFF,           // cullMask
        0,              // sbtRecordOffset
        0,              // sbtRecordStride
        0,              // missIndex
        samplePosition.xyz,     // ray origin
        tMin,           // ray min range
        sampleVector,  // ray direction
        tMax,           // ray max range
        1               // payload (location = 0)
      );
      float cosTheta = dot(normal.xyz, sampleVector);

      glossyIndirectLight = glossyIndirectLight + ((1.0/M_PI) * unlitColor.xyz * payload.color * cosTheta);
    }
    outColor.xyz = outColor.xyz + (glossyIndirectLight / float(globalDrawData.maxDiffuseSampleCount));

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), outColor);
}