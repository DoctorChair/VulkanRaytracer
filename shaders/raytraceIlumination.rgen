
#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_GOOGLE_include_directive : enable

#include "BRDF.glsl"

struct SunLight
{
	vec3 direction;
	vec4 color;
};

struct PointLight
{
	vec3 position;
	vec4 color;
	float strength;
};

struct SpotLight
{
	vec3 position;
	vec3 direction;
	float openingAngle;
	vec4 color;
	float strength;
};

struct Material
{
	uint albedoIndex;
	uint metallicIndex;
	uint normalIndex;
	uint roughnessIndex;	
};

struct drawInstanceData
{
	mat4 modelMatrix;
	Material material;
	uint ID;
};


layout(set = 0, binding = 0) uniform  CameraBuffer{
	mat4 viewMatrix;
	mat4 inverseViewMatrix;
  mat4 projectionMatrix;
  mat4 inverseProjectionMatrix;
	vec3 cameraPosition;
} cameraData;

layout(set = 0, binding = 1) uniform  RenderBuffer{
	float fog;
	uint sunLightCount;
	uint pointLightCount;
	uint spotLightCount;
  uint maxRecoursionDepth;
  uint maxDiffuseSampleCount;
  uint maxSpecularSampleCount;
  uint maxShadowRaySampleCount;
} globalDrawData;


layout(set = 1, binding = 0) uniform sampler2D primaryRayLitColorTexture;
layout(set = 1, binding = 1) uniform sampler2D primaryRayNormalTexture;
layout(set = 1, binding = 2) uniform sampler2D primaryRayDepthTexture;
layout(set = 1, binding = 3) uniform sampler2D primaryRayRoughnessMetalicTexture;
layout(set = 1, binding = 4) uniform sampler2D primaryRayUnlitColorTexture;

layout(set = 2, binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(set = 2, binding = 1, rgba32f) uniform image2D image;

layout(std430, set = 3, binding = 0) readonly buffer SunBuffer{

	SunLight sunLights[];
} sunLightBuffer;

layout(std430, set = 3, binding = 1) readonly buffer PointBuffer{

	PointLight pointLights[];
} pointLightBuffer;

layout(std430, set = 3, binding = 2) readonly buffer SpotBuffer{

	SpotLight spotLights[];
} spotLightBuffer; 

layout(set = 4, binding = 0) uniform sampler albedoSampler;
layout(set = 4, binding = 1) uniform sampler metallicSampler;
layout(set = 4, binding = 2) uniform sampler normalSampler;
layout(set = 4, binding = 3) uniform sampler roughnessSampler;

layout(std430, set = 5, binding = 0) readonly buffer DrawInstanceBuffer{
	drawInstanceData instanceData[];
} drawData;

layout(set = 6, binding = 0) uniform texture2D textures[1024]; 



struct hitPayload
{
  bool miss;
  vec3 color;
  vec3 normal;
  vec3 position;
};

layout(location = 0) rayPayloadEXT float shadowed;
layout(location = 1) rayPayloadEXT hitPayload payload;



void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
    
    vec4 litColor = texture(primaryRayLitColorTexture, inUV);
    vec4 unlitColor = texture(primaryRayUnlitColorTexture, inUV);
    vec4 normal = texture(primaryRayNormalTexture, inUV);
    float depth = texture(primaryRayDepthTexture, inUV).x;
    vec4 roughnessMetalness = texture(primaryRayRoughnessMetalicTexture, inUV);
	
    normal.xyz = normalize(normal.xyz);
	  float reflectance = 0.04;	
	  float roughness = roughnessMetalness.x;
	  float metallic = roughnessMetalness.y;

    vec4 clipSpacePosition = vec4(inUV * 2.0 - 1.0, depth, 1.0);
    vec4 viewSpacePosition = cameraData.inverseProjectionMatrix * clipSpacePosition;
    viewSpacePosition = viewSpacePosition / viewSpacePosition.w;
	  vec4 samplePosition = cameraData.inverseViewMatrix * viewSpacePosition;

    uint  rayFlags = gl_RayFlagsOpaqueEXT;
    uint  shadowRayFlags =  gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT;
    float tMin     = 0.001;
    float tMax     = 10000.0;

    vec4 outColor = litColor;
    
    shadowed = 0.0;

    float pointShadow = shadowed;
    float sunShadow = shadowed;

    for(uint i = 0; i < globalDrawData.pointLightCount && !(shadowed == 1.0); i++)
    {
      vec3 lightDirection = pointLightBuffer.pointLights[i].position - samplePosition.xyz;
      float range = length(lightDirection);
      lightDirection = normalize(lightDirection);
      
      float cosTheta = dot(lightDirection, normal.xyz);

      if(cosTheta > 0)
      {
        traceRayEXT(topLevelAS, // acceleration structure
        shadowRayFlags,       // rayFlags
        0xFF,           // cullMask
        0,              // sbtRecordOffset
        0,              // sbtRecordStride
        1,              // missIndex
        samplePosition.xyz,     // ray origin
        tMin,           // ray min range
        lightDirection.xyz,  // ray direction
        range,           // ray max range
        0               // payload (location = 0)
        );
      }

      pointShadow = shadowed;
    }  

    shadowed = 0.0;
    for(uint i = 0; i < globalDrawData.sunLightCount && !(shadowed == 1.0); i++)
    {
      vec3 lightDirection = -sunLightBuffer.sunLights[i].direction;
      float range = tMax;
      lightDirection = normalize(lightDirection);
      
      float cosTheta = dot(lightDirection, normal.xyz);

      if(cosTheta > 0)
      {
        traceRayEXT(topLevelAS, // acceleration structure
        shadowRayFlags,       // rayFlags
        0xFF,           // cullMask
        0,              // sbtRecordOffset
        0,              // sbtRecordStride
        1,              // missIndex
        samplePosition.xyz,     // ray origin
        tMin,           // ray min range
        lightDirection.xyz,  // ray direction
        range,           // ray max range
        0               // payload (location = 0)
        );
      }

      sunShadow = shadowed;
    }  

    outColor.xyz = outColor.xyz * max(sunShadow, pointShadow);

    payload.miss = false;
    payload.position = samplePosition.xyz;
    payload.normal = normal.xyz;
    
    vec3 priorPosition = cameraData.cameraPosition;

    vec3 glossyIndirectLight = vec3(0.0);

    //glossy
    for(uint i = 0; i < globalDrawData.maxRecoursionDepth && !payload.miss; i++)
    {
      vec3 incommingDirection = normalize(payload.position - priorPosition);
      vec3 outgoingdirection = reflect(incommingDirection,  payload.normal);
      priorPosition = payload.position;

      traceRayEXT(topLevelAS, // acceleration structure
        rayFlags,       // rayFlags
        0xFF,           // cullMask
        0,              // sbtRecordOffset
        0,              // sbtRecordStride
        0,              // missIndex
        priorPosition,     // ray origin
        tMin,           // ray min range
        outgoingdirection,  // ray direction
        tMax,           // ray max range
        1               // payload (location = 1)
        );

      if(!payload.miss)
      {
        float distance = length(priorPosition - payload.position);

        glossyIndirectLight = payload.color + glossyIndirectLight;
      }
    }

    vec3 viewDirection = normalize(samplePosition.xyz - cameraData.cameraPosition);
    vec3 incomingGlossyLightDirection = reflect(viewDirection, normal.xyz);
    float irradiance = max(dot(normal.xyz, incomingGlossyLightDirection), 0.0);
    glossyIndirectLight = BRDF(-viewDirection, incomingGlossyLightDirection, normal.xyz, unlitColor.xyz, metallic, roughness, reflectance) * glossyIndirectLight * irradiance;

    outColor.xyz = outColor.xyz + glossyIndirectLight;
    
    payload.miss = false;
    payload.normal = normal.xyz;

    
    vec3 diffuseIndiectLight = vec3(0.0, 0.0, 0.0);
    uint diffuseRayCount = 1;
    //diffuse
    for(uint i = 0; i < diffuseRayCount; i++)
    {
      vec3 diffuseDirection = normal.xyz;

      traceRayEXT(topLevelAS, // acceleration structure
        rayFlags,       // rayFlags
        0xFF,           // cullMask
        0,              // sbtRecordOffset
        0,              // sbtRecordStride
        0,              // missIndex
        samplePosition.xyz,     // ray origin
        tMin,           // ray min range
        diffuseDirection,  // ray direction
        tMax,           // ray max range
        1               // payload (location = 1)
        );

      if(!payload.miss)
      {
        float distance = length(samplePosition.xyz - payload.position);
        float attenuation = 1.0/(distance*distance);

        diffuseIndiectLight = payload.color + diffuseIndiectLight;
      }
    }
    diffuseIndiectLight = BRDF(-viewDirection, normal.xyz, normal.xyz, unlitColor.xyz, metallic, roughness, reflectance) * diffuseIndiectLight;

    outColor.xyz = outColor.xyz + diffuseIndiectLight;

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), outColor);
}